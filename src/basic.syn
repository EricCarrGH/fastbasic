#
# FastBasic - Fast basic interpreter for the Atari 8-bit computers
# Copyright (C) 2017,2018 Daniel Serpell
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program.  If not, see <http://www.gnu.org/licenses/>
#

# Syntax in simplified PEG format
# -------------------------------

TOKENS {
 # First token must be "END"
 TOK_END
 # Constant and variable loading
 TOK_NUM, TOK_BYTE, TOK_CSTRING, TOK_CDATA, TOK_VAR_ADDR, TOK_VAR_LOAD
 TOK_SHL8, TOK_0, TOK_1
 # PUSH plus composed PUSH ops
 TOK_PUSH, TOK_PUSH_VAR_LOAD
 TOK_PUSH_BYTE, TOK_PUSH_0, TOK_PUSH_1
 # Numeric operators
 TOK_NEG, TOK_ABS, TOK_SGN, TOK_ADD, TOK_SUB, TOK_MUL, TOK_DIV, TOK_MOD
 # Bitwise operators
 TOK_BIT_AND, TOK_BIT_OR, TOK_BIT_EXOR
 # Functions
 TOK_PEEK, TOK_DPEEK
 TOK_TIME, TOK_FRE, TOK_RAND
 # Boolean operators
 TOK_L_NOT, TOK_L_OR, TOK_L_AND
 # Comparisons
 TOK_LT, TOK_GT, TOK_NEQ, TOK_EQ
 # Convert from int to bool
 TOK_COMP_0
 # Low level statements
 TOK_POKE, TOK_DPOKE, TOK_MOVE, TOK_NMOVE, TOK_MSET, TOK_INC, TOK_DEC
 TOK_VAR_STORE
 # Graphic support statements
 TOK_GRAPHICS, TOK_PLOT, TOK_DRAWTO
 # Print statements
 TOK_PRINT_STR, TOK_PRINT_TAB, TOK_PRINT_EOL
 # I/O
 TOK_GETKEY, TOK_INPUT_STR, TOK_XIO, TOK_CLOSE, TOK_GET, TOK_PUT
 TOK_BPUT, TOK_BGET
 # Set's IO channel (before PRINT/INPUT/PUT/GET)
 TOK_IOCHN
 # Jumps
 TOK_JUMP, TOK_CJUMP, TOK_CALL, TOK_RET, TOK_CRET
 # FOR loop support
 TOK_FOR, TOK_FOR_START, TOK_FOR_NEXT, TOK_FOR_EXIT
 # Arrays
 TOK_DIM, TOK_USHL
 # Strings
 TOK_COPY_STR, TOK_VAL, TOK_CMP_STR, TOK_INT_STR, TOK_STR_IDX, TOK_CAT_STR, TOK_CHR
 # Sound off - could be implemented as simple POKE expressions, but it's shorter this way
 TOK_SOUND_OFF
 TOK_PAUSE
 # USR, calls ML routinr
 TOK_USR_ADDR, TOK_USR_PARAM, TOK_USR_CALL

#@if FASTBASIC_FP
 # Floating point computations
 TOK_INT_FP, TOK_FP_VAL, TOK_FP_SGN, TOK_FP_ABS, TOK_FP_NEG, TOK_FLOAT
 TOK_FP_DIV, TOK_FP_MUL, TOK_FP_SUB, TOK_FP_ADD, TOK_FP_STORE, TOK_FP_LOAD
 TOK_FP_EXP, TOK_FP_EXP10, TOK_FP_LOG, TOK_FP_LOG10, TOK_FP_INT, TOK_FP_CMP
 TOK_FP_IPOW, TOK_FP_RND, TOK_FP_SQRT, TOK_FP_SIN, TOK_FP_COS, TOK_FP_ATN
 TOK_FP_STR
#@endif FASTBASIC_FP
}

EXTERN {
 E_REM, E_NUMBER_WORD, E_NUMBER_BYTE, E_EOL
 E_PUSH_LT, E_POP_LOOP, E_POP_REPEAT
 E_POP_IF, E_ELSE, E_ELIF, E_EXIT_LOOP
 E_POP_WHILE, E_POP_FOR, E_POP_PROC_1, E_POP_PROC_2, E_POP_DATA
 E_CONST_STRING
 E_VAR_CREATE, E_VAR_SET_TYPE
 E_VAR_WORD, E_VAR_ARRAY_BYTE, E_VAR_ARRAY_WORD, E_VAR_STRING, E_VAR_ARRAY_STRING
 E_LABEL, E_LABEL_DEF
 E_PUSH_VAR, E_POP_VAR

#@if FASTBASIC_FP
 E_VAR_FP, E_NUMBER_FP
#@endif FASTBASIC_FP
}

# Floating point expressions
#@if FASTBASIC_FP
FP_EXPR:
        FP_T_EXPR FP_E_EXPR_MORE FP_M_EXPR_MORE FP_EXPR_MORE

FP_EXPR_MORE: '+', '-'
        "+" FP_T_EXPR FP_E_EXPR_MORE FP_M_EXPR_MORE emit TOK_FP_ADD FP_EXPR_MORE
        "-" FP_T_EXPR FP_E_EXPR_MORE FP_M_EXPR_MORE emit TOK_FP_SUB FP_EXPR_MORE
        pass

FP_M_EXPR_MORE: '*', '/'
        "*" FP_T_EXPR FP_E_EXPR_MORE emit TOK_FP_MUL FP_M_EXPR_MORE
        "/" FP_T_EXPR FP_E_EXPR_MORE emit TOK_FP_DIV FP_M_EXPR_MORE
        pass

FP_E_EXPR_MORE: '^'
        "^" T_EXPR emit TOK_FP_IPOW FP_E_EXPR_MORE
        "^" emit TOK_FP_LOG10 FP_T_EXPR emit TOK_FP_MUL emit TOK_FP_EXP10 FP_E_EXPR_MORE
        pass

FP_T_EXPR: floating point constant, variable or function
	emit TOK_FLOAT E_NUMBER_FP
        "-" FP_T_EXPR emit TOK_FP_NEG
        "+" FP_T_EXPR
        # FP Functions
        "ABS"   FP_PAR_EXPR emit TOK_FP_ABS
        "ATN"   FP_PAR_EXPR emit TOK_FP_ATN
        "SGN"   FP_PAR_EXPR emit TOK_FP_SGN
        "EXP10" FP_PAR_EXPR emit TOK_FP_EXP10
        "EXP"   FP_PAR_EXPR emit TOK_FP_EXP
        "LOG10" FP_PAR_EXPR emit TOK_FP_LOG10
        "LOG"   FP_PAR_EXPR emit TOK_FP_LOG
        "SQR"   FP_PAR_EXPR emit TOK_FP_SQRT
        "SIN"   FP_PAR_EXPR emit TOK_FP_SIN
        "COS"   FP_PAR_EXPR emit TOK_FP_COS
        "VAL"   STR_PAR_EXPR emit TOK_FP_VAL
        "RND()" emit TOK_FP_RND
        FP_PAR_EXPR
	emit TOK_VAR_ADDR E_VAR_FP "%" emit TOK_FP_LOAD
        INT_EXPR emit TOK_INT_FP

FP_PAR_EXPR: left parenthesis
        "(" FP_EXPR ")"
#@endif FASTBASIC_FP

# Normal expressions
INT_EXPR: integer expression
	T_EXPR BIT_EXPR_MORE M_EXPR_MORE INT_EXPR_MORE

PUSH_T_EXPR:
        emit TOK_PUSH T_EXPR

INT_EXPR_MORE: '+', '-'
	"+" PUSH_T_EXPR BIT_EXPR_MORE M_EXPR_MORE emit TOK_ADD INT_EXPR_MORE
	"-" PUSH_T_EXPR BIT_EXPR_MORE M_EXPR_MORE emit TOK_SUB INT_EXPR_MORE
        pass

M_EXPR_MORE: '*', '/', 'MOD'
	"*"   PUSH_T_EXPR BIT_EXPR_MORE emit TOK_MUL M_EXPR_MORE
	"/"   PUSH_T_EXPR BIT_EXPR_MORE emit TOK_DIV M_EXPR_MORE
	"MOD" PUSH_T_EXPR BIT_EXPR_MORE emit TOK_MOD M_EXPR_MORE
        pass

BIT_EXPR_MORE: '&', '!', 'EXOR'
        "&"    PUSH_T_EXPR emit TOK_BIT_AND  BIT_EXPR_MORE
        "!"    PUSH_T_EXPR emit TOK_BIT_OR   BIT_EXPR_MORE
        "EXOR" PUSH_T_EXPR emit TOK_BIT_EXOR BIT_EXPR_MORE
        pass

NEG_EXPR:
	"-" T_EXPR
	"+" NEG_EXPR
	T_EXPR emit TOK_NEG

ADR_EXPR: string or variable
        STR_EXPR
        emit TOK_VAR_LOAD E_VAR_ARRAY_WORD
        emit TOK_VAR_LOAD E_VAR_ARRAY_BYTE
        emit TOK_VAR_LOAD E_VAR_ARRAY_STRING

USR_EXPR_MORE:
        "," EXPR emit TOK_USR_PARAM USR_EXPR_MORE
        pass

T_EXPR: integer constant, variable or function
	emit TOK_BYTE E_NUMBER_BYTE
	emit TOK_NUM E_NUMBER_WORD
        "-" NEG_EXPR
        "+" T_EXPR
        "NOT" NOT_EXPR emit TOK_L_NOT
        # Special (predefined) variables
        "TIME"   emit TOK_TIME
        # Variables as R-Values, push value into stack
        ARRAY_WORD_ADDR emit TOK_DPEEK
        VAR_BYTE_LVALUE emit TOK_PEEK
	emit TOK_VAR_LOAD E_VAR_WORD
        # Functions
        "ABS"    PAR_EXPR emit TOK_ABS
        "SGN"    PAR_EXPR emit TOK_SGN
        "PEEK"   PAR_EXPR emit TOK_PEEK
        "PADDLE" emit TOK_NUM word PADDL0 emit TOK_PUSH_BYTE emit 7 RD_PORT
        "PTRIG"  emit TOK_NUM word PTRIG0 emit TOK_PUSH_BYTE emit 7 RD_PORT
        "STICK"  emit TOK_NUM word STICK0 emit TOK_PUSH_BYTE emit 3 RD_PORT
        "STRIG"  emit TOK_NUM word STRIG0 emit TOK_PUSH_BYTE emit 3 RD_PORT
#@if FASTBASIC_FP
        "INT"    FP_PAR_EXPR emit TOK_FP_INT
#@endif FASTBASIC_FP
        "RAND"   PAR_EXPR emit TOK_RAND
        "DPEEK"  PAR_EXPR emit TOK_DPEEK
        "FRE()"  emit TOK_FRE
        "ERR()"  emit TOK_BYTE emit IOERROR emit TOK_PEEK
        "USR("   EXPR emit TOK_USR_ADDR emit TOK_PUSH USR_EXPR_MORE ")" emit TOK_USR_CALL
        "ADR("   ADR_EXPR ")"
        "LEN"    STR_PAR_EXPR emit TOK_PEEK # First byte of string is the length
        "VAL"    STR_PAR_EXPR emit TOK_VAL
        "ASC"    STR_PAR_EXPR emit TOK_PUSH_1 emit TOK_ADD emit TOK_PEEK # TODO: does not check for empty strings.
        "KEY()"  emit TOK_NUM word CH emit TOK_PEEK emit TOK_PUSH_BYTE emit 255 emit TOK_BIT_EXOR
        PAR_EXPR

# Used to handle PADDLE/STICK/PTRIG/STRIG
RD_PORT:
        emit TOK_PUSH PAR_EXPR emit TOK_BIT_AND emit TOK_ADD emit TOK_PEEK

PAR_EXPR: left parenthesis
        "(" EXPR ")"

STR_PAR_EXPR: left parenthesis
        "(" STR_EXPR ")"

# Parses a continuation of an INT to BOOLean expression
OR_AND_BOOL:
        OR_EXPR_RIGHT
        AND_EXPR_RIGHT

# Test if an INT needs to be converted to BOOL
TEST_BOOL_EXPR:
        emit TOK_COMP_0 OR_AND_BOOL
        COMP_EXPR_RIGHT OR_EXPR_MORE AND_EXPR_MORE COMP_EXPR_MORE
#@if FASTBASIC_FP
        emit TOK_INT_FP COMP_FP_RIGHT  OR_EXPR_MORE AND_EXPR_MORE COMP_EXPR_MORE
#@endif FASTBASIC_FP
        pass

# General Expression - Can be INT or BOOL
EXPR:
        INT_EXPR TEST_BOOL_EXPR
        STR_EXPR COMP_STR_RIGHT
#@if FASTBASIC_FP
        FP_EXPR COMP_FP_RIGHT
#@endif FASTBASIC_FP

# Forced BOOL expressions, convert to BOOL always
FORCE_BOOL_EXPR:
        NOT_EXPR AND_EXPR_MORE OR_EXPR_MORE

OR_EXPR_RIGHT: 'OR'
        "OR" emit TOK_PUSH NOT_EXPR AND_EXPR_MORE emit TOK_L_OR OR_EXPR_MORE

OR_EXPR_MORE:
        OR_EXPR_RIGHT
        pass

AND_EXPR_RIGHT: 'AND'
        "AND" emit TOK_PUSH NOT_EXPR emit TOK_L_AND AND_EXPR_MORE

AND_EXPR_MORE:
        AND_EXPR_RIGHT
        pass

NOT_EXPR:
        "NOT" NOT_EXPR emit TOK_L_NOT
        INT_EXPR COMP_OR_BOOL
#@if FASTBASIC_FP
        FP_EXPR COMP_FP_RIGHT
#@endif FASTBASIC_FP
        STR_EXPR COMP_STR_RIGHT

COMP_OR_BOOL:
        COMP_EXPR_RIGHT COMP_EXPR_MORE
#@if FASTBASIC_FP
        emit TOK_INT_FP COMP_FP_RIGHT COMP_EXPR_MORE
#@endif FASTBASIC_FP
        emit TOK_COMP_0

COMP_EXPR_RIGHT: integer comparison operator
        "<=" emit TOK_PUSH INT_EXPR emit TOK_GT emit TOK_L_NOT
        ">=" emit TOK_PUSH INT_EXPR emit TOK_LT emit TOK_L_NOT
        "<>" emit TOK_PUSH INT_EXPR emit TOK_NEQ
        "<"  emit TOK_PUSH INT_EXPR emit TOK_LT
        ">"  emit TOK_PUSH INT_EXPR emit TOK_GT
        "="  emit TOK_PUSH INT_EXPR emit TOK_EQ

#@if FASTBASIC_FP
# FP Comparisons:
COMP_FP_RIGHT: floating-point comparison operator
        "="  FP_EXPR emit TOK_FP_CMP emit TOK_EQ
        ">"  FP_EXPR emit TOK_FP_CMP emit TOK_GT
        ">=" FP_EXPR emit TOK_FP_CMP emit TOK_LT emit TOK_L_NOT
        "<>" FP_EXPR emit TOK_FP_CMP emit TOK_NEQ
        "<=" FP_EXPR emit TOK_FP_CMP emit TOK_GT emit TOK_L_NOT
        "<"  FP_EXPR emit TOK_FP_CMP emit TOK_LT
#@endif FASTBASIC_FP

COMP_STR_RIGHT: comparison operator
        "="  emit TOK_PUSH STR_EXPR emit TOK_CMP_STR emit TOK_EQ
        ">"  emit TOK_PUSH STR_EXPR emit TOK_CMP_STR emit TOK_GT
        ">=" emit TOK_PUSH STR_EXPR emit TOK_CMP_STR emit TOK_LT emit TOK_L_NOT
        "<>" emit TOK_PUSH STR_EXPR emit TOK_CMP_STR emit TOK_NEQ
        "<=" emit TOK_PUSH STR_EXPR emit TOK_CMP_STR emit TOK_GT emit TOK_L_NOT
        "<"  emit TOK_PUSH STR_EXPR emit TOK_CMP_STR emit TOK_LT

COMP_EXPR_MORE:
        COMP_EXPR_RIGHT COMP_EXPR_MORE
        pass

# String expressions: base plus optional index
STR_EXPR: string expression
        STR_EXPR_BASE OPT_STR_INDEX

OPT_STR_INDEX:
        "[" emit TOK_PUSH EXPR C_EXPR "]" emit TOK_STR_IDX OPT_STR_INDEX
        "[" emit TOK_PUSH EXPR "]" emit TOK_PUSH_BYTE emit 255 emit TOK_STR_IDX OPT_STR_INDEX
        pass

STR_EXPR_BASE:
        # Either, a constant string
        "\"" emit TOK_CSTRING E_CONST_STRING
        # Or a string function
        "CHR$" PAR_EXPR emit TOK_CHR
        "STR$" PAR_EXPR emit TOK_INT_STR
#@if FASTBASIC_FP
        "STR$" FP_PAR_EXPR emit TOK_FP_STR
#@endif FASTBASIC_FP
        # Or a string variable
        emit TOK_VAR_LOAD E_VAR_STRING "$"
        # Or a string array
        ARRAY_STRING_ADDR emit TOK_DPEEK

# Statements
PRINT_SEP:
        "," emit TOK_PRINT_TAB
        ";"
        E_EOL emit TOK_PRINT_EOL

PRINT_ONE:
	EXPR emit TOK_INT_STR emit TOK_PRINT_STR PRINT_SEP
#@if FASTBASIC_FP
	FP_EXPR emit TOK_FP_STR emit TOK_PRINT_STR PRINT_SEP
#@endif FASTBASIC_FP
        STR_EXPR emit TOK_PRINT_STR PRINT_SEP
        "," emit TOK_PRINT_TAB
        ";"

PRINT_EXPR:
        PRINT_ONE PRINT_MORE
        PRINT_SEP

PRINT_MORE:
        PRINT_ONE PRINT_MORE
        pass

# Parse multi-line IF
THEN_OR_MULTILINE:
        "Then" PARSE_STATEMENT E_POP_IF
        pass

VAR_CREATE_TYPE:
        "$" emit VT_STRING
#@if FASTBASIC_FP
        "%" emit VT_FLOAT
#@endif FASTBASIC_FP
        emit VT_WORD

# Parse variable as L-VALUE, stores the *address* in the stack, this creates the
# variable if not exist.
# NOTE: the creation rule (E_VAR_SET_TYPE) always fails, so the parser uses the next
#       rules using the created variable.
VAR_WORD_LVALUE: integer variable
        E_VAR_CREATE VAR_CREATE_TYPE E_VAR_SET_TYPE
        emit TOK_VAR_ADDR E_VAR_WORD
        ARRAY_WORD_ADDR

VAR_STR_LVALUE:
        emit TOK_VAR_ADDR E_VAR_STRING "$"
        ARRAY_STRING_ADDR

VAR_WORD_SAVE: integer variable
        E_VAR_CREATE VAR_CREATE_TYPE E_VAR_SET_TYPE
        E_VAR_WORD E_PUSH_VAR

#@if FASTBASIC_FP
VAR_FP_LVALUE:
        emit TOK_VAR_ADDR E_VAR_FP "%"
#@endif FASTBASIC_FP

# Parse an "=" sign and pushes address into stack.
EQUAL_PUSH: variable assignment
        "=" emit TOK_PUSH

# Parse optional "STEP" in for
STEP_OPTIONAL:
        "Step" emit TOK_PUSH EXPR
        emit TOK_PUSH_1

# Parse variable name in "NEXT"
NEXT_VARNAME:
        E_VAR_WORD
        E_VAR_ARRAY_WORD
        emit 0 pass

# POSITION arguments, used also in PLOT, DRAWTO, LOCATE and FILLTO
POSITION:
        emit TOK_BYTE emit COLCRS emit TOK_PUSH EXPR emit TOK_DPOKE emit TOK_BYTE emit ROWCRS C_EXPR emit TOK_POKE

# SOUND arguments, three possibilities:
#  SOUND voice, freq, distort, vol
#  SOUND voice
#  SOUND
SOUND:
        EXPR emit TOK_USHL emit TOK_PUSH emit TOK_NUM word AUDF1 emit TOK_ADD C_EXPR EXPR_AB emit TOK_SHL8 emit TOK_ADD emit TOK_DPOKE emit TOK_NUM word AUDCTL emit TOK_PUSH_0 emit TOK_POKE emit TOK_NUM word SKCTL emit TOK_PUSH_BYTE emit 3 emit TOK_POKE
        EXPR emit TOK_USHL emit TOK_PUSH emit TOK_NUM word AUDF1 emit TOK_ADD emit TOK_PUSH_0 emit TOK_DPOKE
        emit TOK_SOUND_OFF

# Word Arrays
ARRAY_WORD_ADDR:
	emit TOK_VAR_LOAD E_VAR_ARRAY_WORD emit TOK_PUSH PAR_EXPR emit TOK_USHL emit TOK_ADD

# String Arrays
ARRAY_STRING_ADDR:
	emit TOK_VAR_LOAD E_VAR_ARRAY_STRING "$" emit TOK_PUSH PAR_EXPR emit TOK_USHL emit TOK_ADD

# This is the same as "ARRAY_BYTE_ADDR"
VAR_BYTE_LVALUE:
	emit TOK_VAR_LOAD E_VAR_ARRAY_BYTE emit TOK_PUSH PAR_EXPR emit TOK_ADD

# DIM
DIM_MORE: comma
        "," DIM_VAR DIM_MORE
        pass

# INPUT
INPUT_STR:
        IO_CHAN emit TOK_IOCHN ","                       # I/O channel, don't print prompt
        "\"" emit TOK_CSTRING E_CONST_STRING emit TOK_PRINT_STR PRINT_SEP # Prints a given string
        PRINT_SEP                                        # If starts with ',' or ';', don't print anyting
        emit TOK_BYTE emit 63 emit TOK_PUT               # Prints a '?' by default

INPUT_VAR_LIST:
        INPUT_VAR INPUT_VAR_MORE

INPUT_VAR_MORE:
        "," INPUT_VAR_LIST
        pass

INPUT_VAR:
        VAR_WORD_LVALUE emit TOK_PUSH emit TOK_INPUT_STR emit TOK_VAL emit TOK_DPOKE
        VAR_BYTE_LVALUE emit TOK_PUSH emit TOK_INPUT_STR emit TOK_VAL emit TOK_POKE
        VAR_STR_LVALUE  emit TOK_PUSH emit TOK_INPUT_STR emit TOK_COPY_STR
#@if FASTBASIC_FP
        VAR_FP_LVALUE   emit TOK_PUSH emit TOK_INPUT_STR emit TOK_FP_VAL emit TOK_FP_STORE
#@endif FASTBASIC_FP


# Dim variable types
DIM_VAR_TYPE_NUM: array type
        "Byte"               emit VT_ARRAY_BYTE
        "Word" emit TOK_USHL emit VT_ARRAY_WORD
               emit TOK_USHL emit VT_ARRAY_WORD

DIM_VAR_TYPE:
        "$" PAR_EXPR emit TOK_USHL emit VT_ARRAY_STRING
        PAR_EXPR DIM_VAR_TYPE_NUM

DIM_VAR: new variable name
        emit TOK_VAR_ADDR E_VAR_CREATE emit TOK_PUSH DIM_VAR_TYPE E_VAR_SET_TYPE emit TOK_DIM

# I/O expressions
IO_CHAN: I/O channel number
        "#" EXPR

IO_CHAN_OPT:
        IO_CHAN emit TOK_IOCHN ","
        pass

# Used only for CLS
IO_CHAN_OPT_NOCOMMA:
        IO_CHAN emit TOK_IOCHN
        pass

# Resets I/O channel to 0
IO_CHAN0:
        emit TOK_0 emit TOK_IOCHN

# Get from keyboard expression
GETK_EXPR: variable name
        VAR_BYTE_LVALUE emit TOK_PUSH emit TOK_GETKEY emit TOK_POKE
        VAR_WORD_SAVE   emit TOK_GETKEY emit TOK_VAR_STORE E_POP_VAR
        VAR_WORD_LVALUE emit TOK_PUSH emit TOK_GETKEY emit TOK_DPOKE

# Get expression
GET_EXPR: variable name
        "," VAR_BYTE_LVALUE emit TOK_PUSH emit TOK_GET emit TOK_POKE GET_EXPR_MORE
        "," VAR_WORD_LVALUE emit TOK_PUSH emit TOK_GET emit TOK_DPOKE GET_EXPR_MORE

GET_EXPR_MORE:
        GET_EXPR
        IO_CHAN0

# A comma followed by an expression
C_EXPR: comma followed by an expression
        "," emit TOK_PUSH EXPR

# Get two comma separated expressions ",A,B" and returns "A*16+B"
EXPR_AB:
        C_EXPR emit TOK_USHL emit TOK_USHL emit TOK_USHL emit TOK_USHL C_EXPR emit TOK_ADD

# Parses a XIO AUX1/AUX2/STRING expression
XIO_EXPR:
        C_EXPR C_EXPR emit TOK_SHL8 emit TOK_ADD emit TOK_PUSH "," STR_EXPR emit TOK_XIO

# Parses a "DATA" expression, get's binary data in memory
DATA_WORDS: data word
        "," E_NUMBER_WORD DATA_WORDS
        pass

DATA_BYTES: data byte
        # To store bytes, simply remove upper part
        "," E_NUMBER_BYTE DATA_BYTES
        pass

DATA_TYPE: data type
        # Note: E_VAR_SET_TYPE skips white space, this allows spaces between
        #       the data type and the "=".
        "Byte" emit VT_ARRAY_BYTE E_VAR_SET_TYPE "=" E_NUMBER_BYTE DATA_BYTES
        "Word" emit VT_ARRAY_WORD E_VAR_SET_TYPE "=" E_NUMBER_WORD DATA_WORDS

DATA_END:
        ","
        E_POP_DATA

DATA_VAR: new array name
        emit TOK_VAR_ADDR E_VAR_CREATE "()" emit TOK_PUSH emit TOK_CDATA emit LT_DATA E_PUSH_LT
        pass

# Parse a line
PARSE_LINE_COMMAND:
	"." E_REM
	"?" IO_CHAN_OPT PRINT_EXPR IO_CHAN0
	"PRint" IO_CHAN_OPT PRINT_EXPR IO_CHAN0
        "INput" INPUT_STR INPUT_VAR_LIST IO_CHAN0
        "GEt" GETK_EXPR
        "GEt" IO_CHAN emit TOK_IOCHN GET_EXPR
        "PUt" IO_CHAN_OPT EXPR emit TOK_PUT IO_CHAN0
        "CLS" IO_CHAN_OPT_NOCOMMA emit TOK_BYTE emit CLS emit TOK_PUT IO_CHAN0
        "Poke"  EXPR C_EXPR emit TOK_POKE
        "Dpoke" EXPR C_EXPR emit TOK_DPOKE
        "MSet"  EXPR C_EXPR C_EXPR emit TOK_MSET
        "Move"  EXPR C_EXPR C_EXPR emit TOK_MOVE
        "-move" EXPR C_EXPR C_EXPR emit TOK_NMOVE
        "DO" emit LT_DO_LOOP E_PUSH_LT
        "Loop" emit TOK_JUMP E_POP_LOOP
        "Repeat" emit LT_REPEAT E_PUSH_LT
        "Until" FORCE_BOOL_EXPR emit TOK_CJUMP E_POP_REPEAT
        "While" emit LT_WHILE_1 E_PUSH_LT FORCE_BOOL_EXPR emit TOK_CJUMP emit LT_WHILE_2 E_PUSH_LT
        "WEnd" emit TOK_JUMP E_POP_WHILE
        "If" FORCE_BOOL_EXPR emit TOK_CJUMP emit LT_IF E_PUSH_LT THEN_OR_MULTILINE
        "For" VAR_WORD_LVALUE EQUAL_PUSH EXPR emit TOK_FOR_START "TO" EXPR STEP_OPTIONAL emit TOK_FOR emit TOK_L_NOT emit TOK_CJUMP emit LT_FOR_2 E_PUSH_LT emit LT_FOR_1 E_PUSH_LT
        "Next" emit TOK_FOR_NEXT emit TOK_CJUMP NEXT_VARNAME E_POP_FOR emit TOK_FOR_EXIT
        "Else" emit TOK_JUMP E_ELSE
        "ELIf" emit TOK_JUMP E_ELIF FORCE_BOOL_EXPR emit TOK_CJUMP emit LT_IF E_PUSH_LT
        "ENDif" E_POP_IF
        "EXit" emit TOK_JUMP E_EXIT_LOOP
        "Graphics" emit TOK_BYTE emit 6 emit TOK_CLOSE EXPR emit TOK_GRAPHICS
        "Color" emit TOK_BYTE emit COLOR emit TOK_PUSH EXPR emit TOK_POKE
        "FColor" emit TOK_NUM word FILDAT emit TOK_PUSH EXPR emit TOK_POKE
        "POSition" POSITION
        "PLot" POSITION emit TOK_PLOT
        "DRawto" POSITION emit TOK_BYTE emit DRAWLN emit TOK_DRAWTO
        "FIllto" POSITION emit TOK_BYTE emit FILLIN emit TOK_DRAWTO
        "SEtcolor" EXPR emit TOK_PUSH emit TOK_NUM word COLOR0 emit TOK_ADD EXPR_AB emit TOK_POKE
        "Sound" SOUND
        "DIm" DIM_VAR DIM_MORE
        "CLose" IO_CHAN emit TOK_CLOSE
        "Open" IO_CHAN emit TOK_PUSH_BYTE emit OPEN XIO_EXPR
        "Xio"  IO_CHAN C_EXPR XIO_EXPR
        "BPut" IO_CHAN C_EXPR C_EXPR emit TOK_BPUT
        "BGet" IO_CHAN C_EXPR C_EXPR emit TOK_BGET
        "PAuse" EXPR emit TOK_PAUSE
        "INC" VAR_WORD_LVALUE emit TOK_INC
        "DEc" VAR_WORD_LVALUE emit TOK_DEC
        "PROc" emit TOK_JUMP emit LT_PROC_1 E_PUSH_LT E_LABEL_DEF emit LT_PROC_2 E_PUSH_LT
        "ENDProc" E_POP_PROC_2 emit TOK_RET E_POP_PROC_1
        "EXEc" emit TOK_CALL E_LABEL
        "DAta" DATA_VAR DATA_TYPE DATA_END
        "END" emit TOK_END
#@if FASTBASIC_FP
        "DEG" emit TOK_BYTE emit DEGFLAG emit TOK_PUSH_1 emit TOK_POKE
        "RAd" emit TOK_BYTE emit DEGFLAG emit TOK_PUSH_0 emit TOK_POKE
#@endif FASTBASIC_FP

PARSE_LINE_ASSIGN:
        VAR_WORD_SAVE   "=" EXPR emit TOK_VAR_STORE E_POP_VAR
        VAR_WORD_LVALUE EQUAL_PUSH EXPR emit TOK_DPOKE
        VAR_BYTE_LVALUE EQUAL_PUSH EXPR emit TOK_POKE
        VAR_STR_LVALUE  EQUAL_PUSH STR_EXPR emit TOK_COPY_STR
        VAR_STR_LVALUE  EQUAL_PUSH "+" STR_EXPR emit TOK_CAT_STR
#@if FASTBASIC_FP
        VAR_FP_LVALUE   EQUAL_PUSH FP_EXPR emit TOK_FP_STORE
#@endif FASTBASIC_FP

SKIP_COMMENT:
	"'" E_REM
        E_EOL
        pass

PARSE_STATEMENT: statement or variable assignment
        PARSE_LINE_COMMAND E_EOL
        PARSE_LINE_ASSIGN
        pass

PARSE_START:
        ":" PARSE_START
        PARSE_STATEMENT SKIP_COMMENT

# vi:syntax=perl
